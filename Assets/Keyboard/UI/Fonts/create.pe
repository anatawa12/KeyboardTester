#!/usr/bin/env fontforge

Open(".NotoMerged-Regular.otf")

map = [  \
  /* 0~7 initial number chars */ \
  0u0030, 0uF000,  \
  0u0031, 0uF001,  \
  0u0032, 0uF002,  \
  0u0033, 0uF003,  \
  0u0034, 0uF004,  \
  0u0035, 0uF005,  \
  0u0036, 0uF006,  \
  0u0037, 0uF007,  \
\
  /* 0u232B: Erase To the Left: ‚å´ */ \
  0u232B, 0uE000,  \
  /* 0u2423: Open Box: &blank;: ‚ê£ */ \
  0u2423, 0uE001,  \
  /* 0uE002 */ \
  /* 1F310: Globe with Meridians: üåê */ \
  0u1F310, 0uE003,  \
  \
  /* Japanese */ \
  /* 0u5C0F: Syou means small in JP: Â∞è */ \
  0u5C0F, 0uE010, \
  /* 0u309B : VOICED SOUND MARK : „Çõ */ \
  0u309B, 0uE011, \
  /* 0u309C : VOICED SOUND MARK : „Çõ */ \
  0u309C, 0uE012, \
  /* to avoid trailing comma */ \
  0 \
]

converted = []

# 0~7 for initial chars
i=0
while ( i + 1 < SizeOf(map) )
  from = map[i]
  to = map[i + 1]
  Print("copying ", from, " to ", to)
  Select(from)
  Copy()
  Select(to)
  Paste()
  CopyFgToBg()
  converted = converted + to
  i += 2
endloop

//////////////// OpJpNextCandidate ////////////////
// 0uE013: OpJpNextCandidate
// 0u5909 0u63DB: Hen-kan: Â§âÊèõ
Select(0u5909)
Copy()
Select(0uE013)
Paste()
width = GlyphInfo("Width")
box = GlyphInfo("BBox")
Scale(50, 0, ($descent + $ascent) / 2)

Select(0u63DB)
Copy()
Select(0u0000)
Paste()
box = GlyphInfo("BBox")
Scale(50, GlyphInfo("Width"), ($descent + $ascent) / 2)
Copy()
Select(0uE013)
PasteInto()
SetWidth(width)

converted = converted + 0uE013
//////////////// OpJpNextCandidate ////////////////


//////////////// OpJpSelectCandidate ////////////////
// 0uE014: OpJpSelectCandidate
// 0u78BA 0u5B9A: Kaku-tei: Á¢∫ÂÆö
Select(0u78BA)
Copy()
Select(0uE014)
Paste()
width = GlyphInfo("Width")
box = GlyphInfo("BBox")
Scale(50, 0, ($descent + $ascent) / 2)

Select(0u5B9A)
Copy()
Select(0u0000)
Paste()
box = GlyphInfo("BBox")
Scale(50, GlyphInfo("Width"), ($descent + $ascent) / 2)
Copy()
Select(0uE014)
PasteInto()
SetWidth(width)

converted = converted + 0uE014
//////////////// OpJpSelectCandidate ////////////////

// 0uE002 is copied from CustomGlyph
converted = converted + 0uE002

SelectAll()

# unselect converted
i = 0
while ( i < SizeOf(converted) )
  SelectFewer(converted[i])
  i++
endloop

Clear()

SetFontNames( \
    "KeyboardInternal", \
    "KeyboardInternal", \
    "KeyboardInternal")

Generate("KeyboardInternal.otf")
